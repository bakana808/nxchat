import org.ajoberstar.grgit.*

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		// Gradle-Git
		classpath 'org.ajoberstar:gradle-git:0.12.0'
	}
}

def repo
try {
	repo = Grgit.open(project.file("."))
} catch (Exception e) {
	repo = null
}

if(!project.hasProperty("git_tag")) {
	if(repo == null) {
		ext.git_tag = "-1.0"
	} else {
		def tags = repo.tag.list()
		def tag
		try {
			// find the tag that matches git repo head
			tag = tags.find{it.commit = repo.head()}
		} catch (Exception e) {
			// get the last tag instead
			tag = tags.getAt(tags.size() - 1)
		}
		ext.git_tag = tag.getName();
	}
}

if(!project.hasProperty("git_hash")) {
	if(repo == null) {
		ext.git_hash = "unknown"
	} else {
		ext.git_hash = repo.head().abbreviatedId
	}
}

group = 'org.hyperfresh.mc-hyperchat'
version = git_tag

ext.git_version = git_tag + "-" + git_hash

repositories {
	mavenCentral()

	// Repository for Bukkit
	maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

dependencies {
	// Apache Commons IO
	compile 'org.apache.commons:commons-io:1.3.2'

	// Bukkit API
	compile 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'

	// JUnit (Unit Tests)
	testCompile 'junit:junit:4.8.1'
}

processResources {
	from("src/main/resources") {
		expand 'version': project.git_version
		include 'plugin.yml'
	}

	from("src/main/resources") {
		exclude 'plugin.yml'
	}
}