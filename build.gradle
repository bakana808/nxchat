import org.ajoberstar.grgit.*

apply plugin: 'java'
apply plugin: 'idea'

def repo = Grgit.open(project.file("."))
def head = repo.head()
def tag = repo.tag.list().find{ it.commit == head}

group = 'org.hyperfresh.mc-hyperchat'
version = tag.getName()

ext.git_version = version + "-" + head.getAbbreviatedId()

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		// Gradle-Git
		classpath 'org.ajoberstar:gradle-git:0.12.0'
	}
}

repositories {
	mavenCentral()

	// Repository for Bukkit
	maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

dependencies {
	// Apache Commons IO
	compile 'org.apache.commons:commons-io:1.3.2'

	// Bukkit API
	compile 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'

	// JUnit (Unit Tests)
	testCompile 'junit:junit:4.8.1'
}

processResources {
	from("src/main/resources") {
		expand 'version': project.git_version
		include 'plugin.yml'
	}

	from("src/main/resources") {
		exclude 'plugin.yml'
	}
}